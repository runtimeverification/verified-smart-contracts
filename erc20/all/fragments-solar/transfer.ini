[transfer]
k:  #dummy
    ~> #assume {INIT_REQUIRES}
INIT_REQUIRES: true
    ;types
    andBool #rangeAddress(TO_ID)
    andBool #rangeUInt(256, VALUE)
    andBool #rangeUInt(256, BAL_FROM)
    andBool #rangeUInt(256, BAL_TO)
    ; SOLAR
    andBool V_CONF_BEFORE             ==K String2Id("V_CONF_BEFORE")
    andBool V_CONF_AFTER              ==K String2Id("V_CONF_AFTER")
    andBool V_BAL_CALLER_BEFORE       ==K String2Id("V_BAL_CALLER_BEFORE")
    andBool V_BAL_TO_BEFORE           ==K String2Id("V_BAL_TO_BEFORE")
    andBool V_BAL_CALLER_AFTER        ==K String2Id("V_BAL_CALLER_AFTER")
    andBool V_BAL_TO_AFTER            ==K String2Id("V_BAL_TO_AFTER")
    andBool V_TRANSFER_OUT            ==K String2Id("V_TRANSFER_OUT")
    ; generic accounts for testing unchanged storage values
    andBool #rangeAddress(ANY_ADDR_1)
    andBool #rangeAddress(ANY_ADDR_2)
    andBool V_ANY_ALLOWANCE_BEFORE    ==K String2Id("V_ANY_ALLOWANCE_BEFORE")
    andBool V_ANY_ALLOWANCE_AFTER     ==K String2Id("V_ANY_ALLOWANCE_AFTER")
    andBool #rangeAddress(ANY_OTHER_ADDR)
    andBool ANY_OTHER_ADDR =/=Int CALLER_ID
    andBool ANY_OTHER_ADDR =/=Int TO_ID
    andBool V_ANY_OTHER_BAL_BEFORE    ==K String2Id("V_ANY_OTHER_BAL_BEFORE")
    andBool V_ANY_OTHER_BAL_AFTER     ==K String2Id("V_ANY_OTHER_BAL_AFTER")

[transfer-success]
    ; precondition
+k: ~> #assume {EXTRA_REQUIRES}
    ~> #saveEthereum V_CONF_BEFORE
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(CALLER_ID))       V_BAL_CALLER_BEFORE
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(TO_ID))           V_BAL_TO_BEFORE
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(ANY_OTHER_ADDR))  V_ANY_OTHER_BAL_BEFORE
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                       #abiCallData("allowance", #address(ANY_ADDR_1), #address(ANY_ADDR_2))  V_ANY_ALLOWANCE_BEFORE
    ~> #assume #var(V_BAL_CALLER_BEFORE) ==S #buf(32, BAL_FROM)
    ~> #assume #var(V_BAL_TO_BEFORE)     ==S #buf(32, BAL_TO)
    ; tested function call
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                           #abiCallData("transfer", #address(TO_ID), #uint256(VALUE))  V_TRANSFER_OUT
    ~> #saveEthereum V_CONF_AFTER
    ; retrieving postcondition data
    ~> #mkCallShortcut V_CONF_AFTER CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(CALLER_ID))       V_BAL_CALLER_AFTER
    ~> #mkCallShortcut V_CONF_AFTER CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(TO_ID))           V_BAL_TO_AFTER
    ~> #mkCallShortcut V_CONF_AFTER CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(ANY_OTHER_ADDR))  V_ANY_OTHER_BAL_AFTER
    ~> #mkCallShortcut V_CONF_AFTER CALLER_ID ACCT_ID
                       #abiCallData("allowance", #address(ANY_ADDR_1), #address(ANY_ADDR_2))  V_ANY_ALLOWANCE_AFTER
    ; postcondition
    ~> #restoreEthereum V_CONF_AFTER
    ; we use #assume instead of #assert here, to bind vars and restrict from postcondition
    ~> #assume #var(V_BAL_CALLER_AFTER)     ==S  #buf(32, BAL_CALLER_AFTER)
    ~> #assume #var(V_BAL_TO_AFTER)         ==S  #buf(32, BAL_TO_AFTER)
    ; check that balances other than FROM and TO are unchanged, and all allowances are unchanged
    ~> #assert #var(V_ANY_OTHER_BAL_BEFORE) ==S  #var(V_ANY_OTHER_BAL_AFTER)
    ~> #assert #var(V_ANY_ALLOWANCE_BEFORE) ==S  #var(V_ANY_ALLOWANCE_AFTER)
    ~> #assert {EXTRA_ENSURES}
    => .
EXTRA_REQUIRES: true
    andBool VALUE <=Int BAL_FROM
EXTRA_ENSURES: true
    &&S (
        (
            ; return true
                (#getStatusCode  ==S EVMC_SUCCESS)
            &&S (#getOutput      ==S #buf(32, 1))
            &&S (#getLog         ==S (OLD_LOG ListItem(#abiEventLog(ACCT_ID, "Transfer", #indexed(#address(CALLER_ID)), #indexed(#address(TO_ID)), #uint256(VALUE)))))
            {STORAGE_UPDATE}
        ) ||S (
            ; revert
            (#getStatusCode =/=S EVMC_SUCCESS)
            ; pre-conditions
            {EXTRA_PRE_CONDITIONS}
        ) ||S (
            // return false
                (#getStatusCode ==S EVMC_SUCCESS) &&S (#getOutput ==S #buf(32, 0))
            &&S (OLD_LOG ==S #getLog)
            &&S (STORAGE ==S #getStorage(ACCT_ID))
            // pre-conditions
            {EXTRA_PRE_CONDITIONS}
        )
    )
EXTRA_PRE_CONDITIONS:
    &&S (
        (
            CALLER_ID ==Int 0
        ) orBool (
            TO_ID ==Int 0
        ) orBool (
            VALUE ==Int 0  /* hkg, non-ERC20-compliant */
        ) orBool (
            BAL_TO +Int VALUE >=Int (2 ^Int 256)
        ) orBool (
            ACCT_ID ==Int TO_ID /* 04_chainlink, non-ERC20-compliant */
        )
    )

[transfer-success-regular]
+EXTRA_REQUIRES:
    // conditions
    andBool CALLER_ID =/=Int TO_ID
STORAGE_UPDATE:
    &&S (BAL_CALLER_AFTER ==Int BAL_FROM -Int VALUE)
    &&S (BAL_TO_AFTER     ==Int BAL_TO   +Int VALUE)

[transfer-success-regular-overflow]
+EXTRA_REQUIRES:
    // conditions
    andBool BAL_TO +Int VALUE <Int (2 ^Int 256)

[transfer-success-self]
+EXTRA_REQUIRES:
    // conditions
    andBool CALLER_ID ==Int TO_ID
STORAGE_UPDATE:
    &&S (STORAGE ==S #getStorage(ACCT_ID))

[transfer-failure]
    ; precondition
+k: ~> #assume {EXTRA_REQUIRES}
    ~> #saveEthereum V_CONF_BEFORE
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                       #abiCallData("balanceOf", #address(CALLER_ID))       V_BAL_CALLER_BEFORE
    ~> #assume #var(V_BAL_CALLER_BEFORE) ==S #buf(32, BAL_FROM)
    ; tested function call
    ~> #mkCallShortcut V_CONF_BEFORE CALLER_ID ACCT_ID
                           #abiCallData("transfer", #address(TO_ID), #uint256(VALUE))  V_TRANSFER_OUT
    ~> #assert {EXTRA_ENSURES}
    => .
EXTRA_REQUIRES: true
    andBool VALUE >Int BAL_FROM
EXTRA_ENSURES: true
    &&S (
            ; revert
                (#getStatusCode =/=S EVMC_SUCCESS)
        ||S (
            ; return false
                (#getStatusCode ==S EVMC_SUCCESS)
            &&S (#getOutput     ==S #buf(32, 0))
            &&S (OLD_LOG        ==S #getLog)
            &&S (STORAGE        ==S #getStorage(ACCT_ID))
        )
    )
